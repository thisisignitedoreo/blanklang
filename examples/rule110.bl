"std.bl" include

// Memory:
// 0 . board_length -- board
// board_length + 1 -- a
// board_length + 2 -- b
// board_length + 3 -- c

macro rule 110 end
macro length 50 end
macro iterations 50 end

macro board_length length 3 + end

1 1 board_length - write

macro print_board
	"[ " .
	3 board_length over <= while
		dup read if "#" . else " " . end
		
		1 +
		board_length over <=
	end

	"]\n" . 

	drop
end

macro read_a 1 board_length + read end
macro read_b 2 board_length + read end
macro read_c 3 board_length + read end

macro write_a 1 board_length + write end
macro write_b 2 board_length + write end
macro write_c 3 board_length + write end

0 read write_a
1 read write_b
2 read write_c

0 iterations over < while
	print_board
	
	1 board_length over < while
		dup
		2 read_a <<
		1 read_b <<
		0 read_c <<
		| |
		rule >> 1 &
		swap write
		
		read_b write_a
		read_c write_b
		dup 2 + read write_c
		
		1 +
		board_length over <
	end drop
	
	1 +
	iterations over <
end drop
