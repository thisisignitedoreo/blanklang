"std.bl" include

// Memory:
// 0 . board_length -- board
// board_length + 1 -- a
// board_length + 2 -- b
// board_length + 3 -- c

proc rule 110 end
proc length 50 end
proc iterations 50 end

proc board_length length 3 + end

1 1 board_length - write

proc print_board
	"[ " .
	3 while board_length over <= do
		dup read if "#" . else " " . end
		
		1 +
	end

	"]\n" . 

	drop
end

proc read_a 1 board_length + read end
proc read_b 2 board_length + read end
proc read_c 3 board_length + read end

proc write_a 1 board_length + write end
proc write_b 2 board_length + write end
proc write_c 3 board_length + write end

0 read write_a
1 read write_b
2 read write_c

0 while iterations over < do
	print_board
	
	1 while board_length over < do
		dup
		2 read_a <<
		1 read_b <<
		0 read_c <<
		| |
		rule >> 1 &
		swap write
		
		read_b write_a
		read_c write_b
		dup 2 + read write_c
		
		1 +
	end drop
	
	1 +
end drop
